cmake_minimum_required(VERSION 3.10)

project(qtcreator-plugin-sphinx VERSION "0.2.5")

set( IDE_SOURCE_TREE $ENV{QTC_SOURCE} CACHE PATH "QTC Source Tree" )
set( IDE_BUILD_TREE  $ENV{QTC_BUILD} CACHE PATH "QTC Build Tree" )

if( "${IDE_SOURCE_TREE}" STREQUAL "" OR NOT EXISTS "${IDE_SOURCE_TREE}" )
    message( FATAL_ERROR "QTC_SOURCE must be configured" )
endif()

if( "${IDE_BUILD_TREE}" STREQUAL "" OR NOT EXISTS "${IDE_BUILD_TREE}")
    message( FATAL_ERROR "QTC_BUILD must be configured" )
endif()


set( QTC_PLUGIN_NAME Sphinx )
set( QTC_PLUGIN_REVISION ${PROJECT_VERSION})


option(WITH_TESTS "Build Tests" ON)
option(WITH_DEBUG_CMAKE "Enabled CMake project debugging functionality (e.g. source file disk checking)" OFF)
option(BUILD_WITH_PCH "Build with precompiled headers" OFF)

# Force C++ standard, do not fall back, do not use compiler extensions
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)


set(CMAKE_INCLUDE_CURRENT_DIR ON)

# Set up Qt stuff:
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)
set(CMAKE_AUTOUIC OFF)

list(APPEND CMAKE_PREFIX_PATH "${IDE_SOURCE_TREE}")
#list(APPEND CMAKE_MODULE_PATH "${IDE_SOURCE_TREE}/lib/cmake/QtCreator")
list(APPEND CMAKE_MODULE_PATH "${IDE_SOURCE_TREE}/lib/cmake")

set(CMAKE_INSTALL_PREFIX "" )
set( CMAKE_INSTALL_MESSAGE "LAZY" ) # one of NEVER, LAZY, ALWAYS

find_package(QtCreator COMPONENTS Core TextEditor ProjectExplorer REQUIRED)
find_package( Qt5 COMPONENTS Test XmlPatterns LinguistTools Widgets REQUIRED)
set(QtX Qt${QT_VERSION_MAJOR})

add_feature_info("Build tests" ${WITH_TESTS} "")

add_qtc_plugin( Sphinx VERSION ${qtcreator-plugin-sphinx_VERSION}
    PLUGIN_DEPENDS QtCreator::Core QtCreator::TextEditor QtCreator::ProjectExplorer
    DEPENDS  ${QtX}::Core ${QtX}::Gui ${QtX}::Widgets ${QtX}::XmlPatterns QtCreator::qlitehtml
    SOURCES
    editor/SphinxCodeModel.cpp
    qtcreator-sphinx-plugin.cpp
    qtcreator-sphinx-plugin-projectsettings.cpp
    editor/SphinxOutputParser.cpp
    editor/SphinxHtmlPage.cpp
    editor/SphinxPreviewPage.cpp
    editor/SphinxRightPaneWidget.cpp
    editor/SphinxDocument.cpp
    editor/SphinxEditor.cpp
    editor/SphinxEditorFactory.cpp
    editor/SphinxEditorWidget.cpp
    editor/SphinxHighlighter.cpp
    editor/SphinxFormatter.cpp
    editor/SphinxFormatActions.cpp
    editor/SphinxIndenter.cpp
    editor/SphinxCodeStylePreferencesFactory.cpp
    editor/SphinxCompletionAssist.cpp
    editor/SphinxRstcheckHighlighter.cpp
    editor/SphinxRest2Html.cpp
    options/SphinxOptionsPage.cpp
    options/SphinxOptionsWidget.cpp
    options/SphinxSettings.cpp
    editor/SphinxCodeModel.h
    qtcreator-sphinx-plugin.h
    qtcreator-sphinx-plugin_global.h
    qtcreator-sphinx-pluginconstants.h
    qtcreator-sphinx-plugin-projectsettings.h
    editor/SphinxOutputParser.h
    editor/SphinxHtmlPage.h
    editor/SphinxPreviewPage.h
    editor/SphinxRightPaneWidget.h
    editor/SphinxDocument.h
    editor/SphinxEditor.h
    editor/SphinxEditorFactory.h
    editor/SphinxHighlighter.h
    editor/SphinxEditorWidget.h
    editor/SphinxFormatter.h
    editor/SphinxFormatActions.h
    editor/SphinxIndenter.h
    editor/SphinxCodeStylePreferencesFactory.h
    editor/SphinxCompletionAssist.h
    editor/SphinxRstcheckHighlighter.h
    editor/SphinxRest2Html.h
    editor/SphinxRegularExpressions.h
    editor/SphinxRegularExpressions.cpp
    options/SphinxOptionsPage.h
    options/SphinxOptionsWidget.h
    options/SphinxSettings.h
    qtcreator-sphinx-plugin.qrc
)

add_translation_targets(Sphinx INSTALL_DESTINATION "share/qtcreator/translations" LANGUAGES "de"  TARGETS Sphinx)


extend_qtc_plugin(Sphinx
  CONDITION WITH_TESTS
  SOURCES
      tests/tst_formatter.cpp
      tests/tst_completion.cpp
      tests/tst_formatter.h
      tests/tst_completion.h
  DEPENDS  ${QtX}::Test
)


# workaround QTCREATORBUG-24413
if( NOT EXISTS "${IDE_SOURCE_TREE}/include/src/libs/3rdparty/syntax-highlighting/autogenerated/src/lib/State" )
    message( WARNING "Fix for QTCREATORBUG-24413 applied")
    execute_process( COMMAND ${CMAKE_COMMAND} -E create_symlink "${IDE_SOURCE_TREE}/include/src/libs/3rdparty/syntax-highlighting/src/lib/state.h" "${IDE_SOURCE_TREE}/include/src/libs/3rdparty/syntax-highlighting/autogenerated/src/lib/State"
                     WORKING_DIRECTORY "${IDE_SOURCE_TREE}/include/src/libs/3rdparty/syntax-highlighting/autogenerated/src/lib"
    )
endif()

enable_testing()


add_test (NAME test_plugin_install
          COMMAND ${CMAKE_COMMAND} -DCMAKE_INSTALL_PREFIX=${IDE_BUILD_TREE} -P ${CMAKE_BINARY_DIR}/cmake_install.cmake
)

set (GITHUB_BUILD False )

if( NOT ("$ENV[GITHUB_WORKSPACE}" STREQUAL ""))
    set( GITHUB_BUILD True )
endif()

add_test( NAME test_plugin COMMAND ${IDE_BUILD_TREE}/bin/qtcreator -test Sphinx )

set_tests_properties( "test_plugin" PROPERTIES FIXTURES_REQUIRED test_plugin_install )

if ( GITHUB_BUILD )
    set_tests_properties( "test_plugin" PROPERTIES DISABLED True )
endif()


INSTALL( DIRECTORY "share/qtcreator/sphinx" DESTINATION "share/qtcreator" PATTERN "__pycache__" EXCLUDE  )
INSTALL( FILES "share/qtcreator/snippets/sphinx.xml"  DESTINATION "share/qtcreator/snippets" )
